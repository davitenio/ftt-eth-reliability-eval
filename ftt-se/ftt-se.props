// Time
const int T;


// Reliability of the system within T minutes.
P=? [ F=T*SEC_PER_MINUTE !sys_failure ]

// Reliability of the system within T hours.
P=? [ F=T*SEC_PER_HOUR !sys_failure ]

// Reliability of the system within T days.
P=? [ F=T*SEC_PER_DAY !sys_failure ]


// Probability that a failure of the master/switch region caused the system
// failure within T minutes. We specify this as the probability that before
// time T there is no system failure and that then at time T there is a
// master/switch region failure.
P=? [ !sys_failure U<=T*SEC_PER_MINUTE master_switch_region_failure ]

// Probability that a failure of the master/switch region caused the system
// failure within T hours.
P=? [ !sys_failure U<=T*SEC_PER_HOUR master_switch_region_failure ]

// Probability that a failure of the master/switch region caused the system
// failure within T days.
P=? [ !sys_failure U<=T*SEC_PER_DAY master_switch_region_failure ]

// Long-run probability that a failure of the master/switch region caused a
// system failure.
P=? [ !sys_failure U master_switch_region_failure ]

// Probability that a failure of too many slaves caused the system
// failure within T minutes. We specify this as the probability that before
// time T there is no system failure and that then at time T more slaves have
// failed than can be tolerated.
P=? [ !sys_failure U<=T*SEC_PER_MINUTE slaves_exhausted ]

// Probability that a failure of too many slaves caused the system
// failure within T hours.
P=? [ !sys_failure U<=T*SEC_PER_HOUR slaves_exhausted ]

// Probability that a failure of too many slaves caused the system
// failure within T days.
P=? [ !sys_failure U<=T*SEC_PER_DAY slaves_exhausted ]

// Long-run probability that a failure of too many slaves caused a system
// failure.
P=? [ !sys_failure U slaves_exhausted ]

// Expected mission time in minutes. We specify this as the expected amount of
// mission_time_rw reward accumulated until a state is reached that satisifes
// sys_fail.
R{"mission_time_rw_minutes"}=? [ F "sys_fail" ]

// Expected mission time in hours.
R{"mission_time_rw_hours"}=? [ F "sys_fail" ]

// Expected mission time in days.
R{"mission_time_rw_days"}=? [ F "sys_fail" ]
